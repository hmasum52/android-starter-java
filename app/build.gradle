plugins {
    id 'com.android.application'

    // dependency injection
    id 'com.google.dagger.hilt.android'

    //to read key from local.properties
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

android {
    namespace 'github.hmasum52.androidstarterjava'
    compileSdk 34

    defaultConfig {
        applicationId "com.example.androidstarterjava"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // https://developer.android.com/build/building-cmdline#groovy
    // https://developer.android.com/studio/publish/app-signing#secure-shared-keystore
    // for building with github actions
    signingConfigs {
        release {
            storeFile file("../keystore.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")

            // Optional, specify signing versions used
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release // for github action
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
        // https://medium.com/androiddevelopers/5-ways-to-prepare-your-app-build-for-android-studio-flamingo-release-da34616bb946
        buildConfig true // for reading secrets from local.properties
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'


    def nav_version = "2.7.7"
    // Java language implementation
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    // dependency injection
    // https://developer.android.com/training/dependency-injection/hilt-android#groovy
    // https://stackoverflow.com/a/75624885
    implementation "com.google.dagger:hilt-android:2.44"
    annotationProcessor "com.google.dagger:hilt-compiler:2.44"

    // https://github.com/johncarl81/parceler
    implementation 'org.parceler:parceler-api:1.1.12'
    annotationProcessor 'org.parceler:parceler:1.1.12'

    // image view dependencies
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'

    // Maps SDK for Android
    // https://developers.google.com/maps/documentation/android-sdk/start
    implementation 'com.google.android.gms:play-services-maps:18.2.0'

    // retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation "com.squareup.retrofit2:converter-gson:2.11.0"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.1' //for logging http request by retrofit

    // lottie animation
    implementation 'com.airbnb.android:lottie:6.1.0'

    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'

}